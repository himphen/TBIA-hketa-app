plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "hibernate.v2.sunshine"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('secrets.properties').newDataInputStream())

        debug {
            buildConfigField "String", "OPEN_WEATHER_API_KEY", properties.getProperty('open_weather.api_key')
            buildConfigField "String", "GOOGLE_MAP_API_KEY", properties.getProperty('google_map.api_key')
            resValue "string", "GOOGLE_MAP_API_KEY", properties.getProperty("google_map.api_key")
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "OPEN_WEATHER_API_KEY", properties.getProperty('open_weather.api_key')
            buildConfigField "String", "GOOGLE_MAP_API_KEY", properties.getProperty('google_map.api_key')
            resValue "string", "GOOGLE_MAP_API_KEY", properties.getProperty("google_map.api_key")
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
//        https://github.com/Kotlin/kotlinx.coroutines/issues/976
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"]
    }

    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':api-service')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.1'

    implementation 'androidx.activity:activity-ktx:1.2.3'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // https://developer.android.com/jetpack/androidx/releases/leanback
    implementation "androidx.leanback:leanback:$leanback_version"
    implementation "androidx.leanback:leanback-preference:$leanback_version"
    implementation 'androidx.leanback:leanback-tab:1.1.0-beta01'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // https://developer.android.com/training/data-storage/room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    implementation platform('com.google.firebase:firebase-bom:28.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'

    // https://github.com/afollestad/material-dialogs
    implementation 'com.afollestad.material-dialogs:core:3.3.0'

    // https://github.com/InsertKoinIO/koin
    implementation "io.insert-koin:koin-android:$koin_version"

    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.maps.android:maps-utils-ktx:2.4.0'
    implementation 'com.google.maps.android:maps-ktx:3.1.0'

    implementation 'com.github.himphen:logger:3.0.1'

    implementation 'com.github.st235:expandablebottombar:1.4.0'

    // https://github.com/coil-kt/coil
    implementation 'io.coil-kt:coil:1.3.0'
}